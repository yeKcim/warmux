# Project name
PROJECT(Wormux)
SET(WORMUX_MAJOR "0")
SET(WORMUX_MINOR "8")
SET(WORMUX_PATCH "beta1")
SET(VERSION "${WORMUX_MAJOR}.${WORMUX_MINOR}${WORMUX_PATCH}")


# Some aliases of cmake variables
IF(BUILD)
  SET(CMAKE_BUILD_TYPE ${BUILD})
ENDIF(BUILD)
IF(PREFIX)
  SET(CMAKE_INSTALL_PREFIX ${PREFIX})
ENDIF(PREFIX)


# Set some global variables
SET(CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/cmake)
SET(INSTALL_DATADIR ${CMAKE_INSTALL_PREFIX}/share/wormux)
SET(INSTALL_LOCALEDIR ${CMAKE_INSTALL_PREFIX}/share/locale)
SET(FONT_FILE ${INSTALL_DATADIR}/font/DejaVuSans.ttf)


# Optional package CPack
INCLUDE(CPackConfig OPTIONAL)


# Required libraries
FIND_PACKAGE(Gettext REQUIRED)
FIND_PACKAGE(SDL REQUIRED)
FIND_PACKAGE(SDL_image REQUIRED)
FIND_PACKAGE(SDL_mixer REQUIRED)
FIND_PACKAGE(SDL_ttf REQUIRED)
FIND_PACKAGE(SDL_gfx REQUIRED)
FIND_PACKAGE(SDL_net REQUIRED)
FIND_PACKAGE(libxml++ REQUIRED)
ADD_DEFINITIONS(${LIBXMLPP_CFLAGS})


# config.h and compile-flags
CONFIGURE_FILE(${PROJECT_SOURCE_DIR}/src/config.h.cmake ${PROJECT_SOURCE_DIR}/src/config.h)
ADD_DEFINITIONS(-DHAVE_CONFIG_H)
ADD_DEFINITIONS(-DINSTALL_DATADIR=\\"${INSTALL_DATADIR}\\")
ADD_DEFINITIONS(-DINSTALL_LOCALEDIR=\\"${INSTALL_LOCALEDIR}\\")
ADD_DEFINITIONS(-DFONT_FILE=\\"${FONT_FILE}\\")
ADD_DEFINITIONS(-DPACKAGE_VERSION=\\"${VERSION}\\")
ADD_DEFINITIONS(-Werror -Wall)
IF(CMAKE_BUILD_TYPE MATCHES "Debug" OR CMAKE_BUILD_TYPE MATCHES "RelWithDebInfo")
  ADD_DEFINITIONS(-DDEBUG)
ENDIF(CMAKE_BUILD_TYPE MATCHES "Debug" OR CMAKE_BUILD_TYPE MATCHES "RelWithDebInfo")


# Subdirectories
ADD_SUBDIRECTORY(src)
ADD_SUBDIRECTORY(po)


# install data/
# Replace this workaround with INSTALL(DIRECTORY ...) when cmake 2.4.4 is released
INCLUDE(InstallData)
FOREACH(_data_file ${DATA_FILES})
  GET_FILENAME_COMPONENT(_path ${_data_file} PATH)
  INSTALL(FILES data/${_data_file} DESTINATION share/wormux/${_path})
ENDFOREACH(_data_file ${DATA_FILES})
INSTALL(FILES data/wormux.desktop DESTINATION share/applications)

